def releaseMetadata = true

pipeline {
   agent any
   environment {
      projectsToUpdate = "";
   }
   parameters {
      text(name: 'PROJECT_NAME',
      defaultValue: '''''',
      description: 'Name of the metadata project to release [cora-metadata, systemone-metadata, diva-metadata, alvin-metadata]')
      
   }
   
   stages {
      stage('Initialize') {
         steps {
            script {
               // Set display name and description in Jenkins GUI
               currentBuild.displayName = "#${env.BUILD_NUMBER} - ${params.PROJECT_NAME}"
            }
         }
      }
      stage("Validate project name") {
         steps {
            script {
               def allowedProjects = [
               "cora-metadata",
               "systemone-metadata",
               "diva-metadata",
               "alvin-metadata"
               ]
               if (!allowedProjects.contains(params.PROJECT_NAME)) {
                  error "Invalid PROJECT_NAME: '${params.PROJECT_NAME}'. Allowed values: ${allowedProjects.join(', ')}"
               }
            }
         }
      }
      stage("clone") {
         steps {
            script {
               withCredentials([
               string(credentialsId: '9af4f5c5-4a09-4bbe-9436-2c102765d85b', variable: 'TOKEN'),
               string(credentialsId: '04bc33f5-abdd-4486-b43b-a5900f6fc080', variable: 'USERNAME')
               ]) {
                  sh '''
                  rm -rf "$PROJECT_NAME"
                  git clone https://$USERNAME:$TOKEN@github.com/lsu-ub-uu/$PROJECT_NAME.git "$PROJECT_NAME"
                  '''
               }
            }
         }
      }
      stage("Make sure it is not a release commit") {
         steps {
            script {
               dir("${params.PROJECT_NAME}") {
                  def commitMessage = sh(script: "git log -1 --pretty=%B", returnStdout: true).trim()
                  if (commitMessage.contains("[maven-release-plugin]")) {
                     echo("Release commit detected. Aborting.")
                     releaseMetadata = false
                  }
               }
            }
         }
      }
      stage("Release metadata") {
         when {
            expression {
               return releaseMetadata
            }
         }
         steps {
            build job: 'cora-prepare-and-release',
            wait: true,
            parameters: [
            string(name: 'UB_PROJECT_NAME', value: "${params.PROJECT_NAME}"),
            string(name: 'UB_BRANCH_NAME', value: "master"),
            string(name: 'FORCE_RELEASE', value: "1")
            ]
         }
      }
   }
   post {
      success {
         script {
            if (releaseMetadata) {
               build job: 'cora-release-dockers-batch',
               wait: false
            }
         }
      }
      always {
         cleanWs()
      }
   }
}