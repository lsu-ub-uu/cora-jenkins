/*
This pipeline manages the branching and releasing of a CORA project.
It accepts the project name, branch version, new development version, and build type as parameters.
Stages:
  1. 'Initialize' – sets the build name and description in the Jenkins GUI.
  2. 'Branch' – triggers 'cora-branch' to create a release branch and update master with the next SNAPSHOT version.
  3. 'Release' – runs either 'cora-release' or 'cora-release-for-docker' depending on the selected build type.
*/
pipeline {
   agent any
   parameters {
      string( name: 'UB_PROJECT_NAME',
      defaultValue: 'cora-xx',
      description: 'Name of the project to branch and release')
      
      string( name: 'UB_BRANCH_VERSION',
      defaultValue: 'x.y',
      description: 'Version for the branch (usually the one specified as SNAPSHOT in the pom file)')
      
      string( name: 'UB_DEV_BRANCH_VERSION',
      defaultValue: 'x.y',
      description: 'Version to be set as the new SNAPSHOT version on master')
      
      string( name: 'BUILD_TYPE',
      defaultValue: 'maven',
      description: 'The type of artifact to build. The possible values are either maven: normal maven artifacts, or docker: maven artifacts that generate dockers')
   }
   stages {
      stage('Initialize') {
         steps {
            script {
               // Set display name and description in Jenkins GUI
               currentBuild.displayName = "#${env.BUILD_NUMBER} - ${params.UB_PROJECT_NAME}"
               currentBuild.description = "Version: ${params.UB_BRANCH_VERSION}"
            }
         }
      }
	  stage('Branch') {
         steps {
            build job: 'cora-branch',
            parameters: [[$class: 'StringParameterValue', name: 'UB_PROJECT_NAME', value: "${params.UB_PROJECT_NAME}" ],
            [$class: 'StringParameterValue', name: 'UB_BRANCH_VERSION', value: "${params.UB_BRANCH_VERSION}"],
            [$class: 'StringParameterValue', name: 'UB_DEV_BRANCH_VERSION', value: "${params.UB_DEV_BRANCH_VERSION}"]]
         }
      }
      stage('Release') {
         steps {
            script {
               def releaseBranch = "release/${params.UB_BRANCH_VERSION}"
               def commonParams = [
               string(name: 'UB_PROJECT_NAME', value: params.UB_PROJECT_NAME),
               string(name: 'UB_BRANCH', value: releaseBranch)
               ]
               
               if (params.BUILD_TYPE == "docker") {
                  build job: 'cora-release-for-docker', parameters: commonParams
               } else {
                  build job: 'cora-release', parameters: commonParams
               }
            }
         }
      }
   }
}